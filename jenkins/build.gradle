apply plugin: 'groovy'

sourceSets {
	main {
		groovy {
			srcDirs 'jobs', 'src/main/groovy', 'seed'
		}
		resources {
			srcDirs 'declarative-pipeline', 'src/main/resources', 'src/main/bash'
		}
	}
	test {
		resources {
			srcDirs 'src/test/bats'
			srcDirs 'src/test/docs_helper'
		}
	}
}

ext {
	projectCrawlerVersion = "1.0.0.BUILD-SNAPSHOT"
	jacksonVersion = "2.9.5"
}

repositories {
	jcenter()
	mavenCentral()
	maven { url 'https://repo.jenkins-ci.org/releases/' }
	maven { url "https://repo.spring.io/libs-snapshot-local" }
	maven { url "https://repo.spring.io/libs-milestone-local" }
	maven { url "https://repo.spring.io/libs-release-local" }
}

configurations {
	libs
	compile.extendsFrom(libs)
}

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.4.7'
	compile 'org.jenkins-ci.plugins:job-dsl-core:1.58'
	compile 'org.jenkins-ci.plugins:job-dsl:1.58@jar'

	// for init.groovy
	compile 'org.jenkins-ci.plugins:credentials:2.1.13@jar'
	compile 'org.jenkins-ci.plugins:ssh-credentials:1.13@jar'
	compile 'org.jenkins-ci.plugins:groovy:1.30@jar'
	compile 'org.jenkins-ci.main:jenkins-core:2.19.1'
	// for Intellij not to throw exceptions
	compile 'javax.servlet:servlet-api:2.4'

	// for dsl
	libs "org.springframework.cloud.internal:project-crawler:${projectCrawlerVersion}"
	libs "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
	libs "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
	libs "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}"

	testCompile project(":common")
	testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
		exclude module: 'groovy-all'
	}
	testCompile 'org.xmlunit:xmlunit-core:2.6.0'
	testCompile "org.skyscreamer:jsonassert:1.4.0"
}

test {
	testLogging {
		exceptionFormat = 'full'
	}
}

task libs(type: Copy) {
	into "${project.buildDir}/lib"
	from configurations.libs
}

task storeFile {
	doLast {
		File file = new File(project.buildDir, "lib/jcabigithub.properties")
		file.createNewFile()
		file.text = """\
#User-Agent info
JCabi-Version 0.41
JCabi-Build f1055c4
JCabi-Date 2018-06-20 11:00
"""
	}
}

installBatsIfMissing.dependsOn("initalizeSubModules")
bats.dependsOn("installBatsIfMissing")
test.dependsOn("bats")

build.dependsOn libs
build.dependsOn storeFile
storeFile.mustRunAfter(libs)
